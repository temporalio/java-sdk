
plugins {
    id 'com.github.sherter.google-java-format' version '0.9' apply false
}

// Logs list of failed tests after build is done.
subprojects {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenCentral()
    }

    apply plugin: 'com.github.sherter.google-java-format'

    googleJavaFormat {
        toolVersion '1.7'
        include '**/*.java'
        exclude '**/generated/*'
        exclude '**/.idea/**'
    }

    // add a collection to track failedTests
    ext.failedTests = []

    // add a testlistener to all tasks of type Test
    tasks.withType(Test) {
        afterTest { TestDescriptor descriptor, TestResult result ->
            if (result.resultType == org.gradle.api.tasks.testing.TestResult.ResultType.FAILURE) {
                failedTests << ["${descriptor.className}::${descriptor.name}"]
            }
        }
    }

    // print out tracked failed tests when the build has finished
    gradle.buildFinished {
        if (!failedTests.empty) {
            println "Failed tests for ${project.name}:"
            failedTests.each { failedTest ->
                println failedTest
            }
            println ""
        }
    }
}
//logging.captureStandardOutput LogLevel.INFO
